buildscript {
    repositories {
        jcenter()
        maven {
        url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.30.0'
    id "org.sonarqube" version "5.1.0.4882"
}

sonarqube {
    properties {
        property "sonar.projectKey", "OpenAPITools_openapi-style-validator"
        property "sonar.organization", "openapitools"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

wrapper {
    gradleVersion = '7.6.2'
}

allprojects {
    version = '1.12-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'de.marcphilipp.nexus-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        if(project.name != 'openapi-style-validator-maven-plugin') {
            useJUnitPlatform()
        }
    }

    repositories {
        mavenCentral()
    }

    group = 'org.openapitools.openapistylevalidator'

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.16'
        testCompileOnly 'org.junit.jupiter:junit-jupiter-params:5.11.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    }

    signing {
        useGpgCmd()
        sign(publishing.publications)
    }

    tasks.withType(Sign) {
        onlyIf {
            project.hasProperty('signing.gnupg.keyName')
        }
    }

    nexusPublishing {
        repositories {
            sonatype {
                username = System.getenv('SONATYPE_USER') ?: ''
                password = System.getenv('SONATYPE_PASSWORD') ?: ''
            }
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    check.dependsOn jacocoTestReport
}

nexusStaging {
    packageGroup = 'org.openapitools'
    username = System.getenv('SONATYPE_USER') ?: ''
    password = System.getenv('SONATYPE_PASSWORD') ?: ''
}
